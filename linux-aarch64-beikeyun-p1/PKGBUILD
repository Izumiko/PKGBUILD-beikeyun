# AArch64 multi-platform
# PKGBUILD Maintainer: Izumiko <yosoro@outlook.com>

pkgname=linux-aarch64-current-beikeyun-p1
pkgver=5.7.15_20.08
pkgrel=1
pkgdesc="The Linux Kernel and modules - AArch64 for Beikeyun P1. This package will fetch a Debian built kernel from armbian."
arch=('aarch64')
url="https://mirrors.tuna.tsinghua.edu.cn/armbian/pool/main/l/"
license=('GPL')
options=('!strip')
depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio>=0.7')
optdepends=('uboot-tools')
provides=("linux=${pkgver}" "linux-aarch64=${pkgver}")
conflicts=('linux' 'linux-aarch64' 'linux-aarch64-rc' 'linux-aarch64-beikeyun-p1')
replaces=('linux-armv8')
backup=("etc/mkinitcpio.d/${pkgname}.preset")
install=${pkgname}.install

source=('rk3328-beikeyun-1200mhz.dtb'
        'rk3328-beikeyun-1296mhz.dtb'
        'rk3328-beikeyun-1392mhz.dtb'
        'linux-aarch64.preset'
        '60-linux-aarch64.hook'
        '90-linux-aarch64.hook'
        'linux-aarch64-current-beikeyun-p1.install')
sha256sums=('f482d98fb4dac30fbab34238bd5f5eb464a26944662d5ce8997c1d5f0a4b9122'
            '34f8c63d905643ad3582e39f2f1c7434f8e20ece0643c1b998076dc180cd4ab6'
            'c6c91e0001e8ffd4c02ee6b216f5e6bd1c7e93a63bfdb96c243ccc5b6fcd9497'
            '1b7d0e099b28a325c0468ad22f32caf9266c943a3af06ffffe84b8d058301874'
            'b5ca41cf346f648592ab057c1485b625fe9d947ac3a36a472f498a34fe3040d8'
            '9d502526f4ce65ddb7efde85309d37593aa2c825aa9d630c676d4c4884a6d25b'
            'ca3d0e67f751d3764f4dd8897a3e8fbc1bd2028472a97b2eefc4981c98a90fc8')


prepare() {
  cd "${srcdir}"
  wget -O Packages "https://mirrors.tuna.tsinghua.edu.cn/armbian/dists/buster/main/binary-arm64/Packages"
  debver=$(cat Packages |grep "Package: linux-image-current-rockchip64" -A 10|grep Filename|cut -f2 -d'_'|sort -r|head -n 1)
  debfile=$(cat Packages |grep "Package: linux-image-current-rockchip64" -A 10|grep ${debver}|grep Filename|cut -f2 -d' ')
  kerver=$(echo ${debfile}|cut -d'-' -f2)
  md5sum=$(cat Packages |grep "Package: linux-image-current-rockchip64" -A 16|grep ${debfile} -A 6|grep MD5sum | cut -f2 -d' ')
  imagename=$(echo $debfile|cut -f5 -d'/')
  
  echo "${md5sum}  ${imagename}" > pkg.md5
  echo "${kerver}_${debver}" > pkgver
  
  #download package
  wget -O ${imagename} "https://mirrors.tuna.tsinghua.edu.cn/armbian/${debfile}"
  md5sum -c pkg.md5 || exit -1
  rm Packages pkg.md5

  # extract the prebuilt kernel files
  ar xf "${imagename}"
  tar -xf data.tar.xz
  rm ${imagename} data.tar.xz
}

pkgver() {
  pkgver=$(cat ${srcdir}/pkgver)
  printf "%s" ${pkgver//-/_}
}

package() {
  kerver=$(echo ${pkgver}|cut -f1 -d'_')
  cd "${srcdir}"

  #install dtb file for Beikeyun P1
  install -D -m644 "${srcdir}/rk3328-beikeyun-1200mhz.dtb" "${pkgdir}/boot/rk3328-beikeyun-1200mhz.dtb"
  install -D -m644 "${srcdir}/rk3328-beikeyun-1296mhz.dtb" "${pkgdir}/boot/rk3328-beikeyun-1296mhz.dtb"
  install -D -m644 "${srcdir}/rk3328-beikeyun-1392mhz.dtb" "${pkgdir}/boot/rk3328-beikeyun-1392mhz.dtb"

  # copy boot files  
  cp -r boot/* "${pkgdir}/boot/"
  cd ${pkgdir}/boot
  ln -sf `ls vmlinu*` Image
  ln -sf /usr/lib/linux-image-current-rockchip64 dtb
  ln -sf rk3328-beikeyun-1200mhz.dtb rk3328-beikeyun.dtb
  cd "${srcdir}"

  # copy kernel files
  mkdir -p "${pkgdir}/usr"
  mkdir -p "${pkgdir}/lib"
  cp -r usr "${pkgdir}/"
  cp -r lib "${pkgdir}/"
  
  # set correct depmod command for install
  sed -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${kerver}-rockchip64/g" -i "${startdir}/${pkgname}.install"

  # install mkinitcpio preset file for kernel
  install -D -m644 "${srcdir}/linux-aarch64.preset" "${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset"
  sed \
    -e "1s|'linux.*'|'${pkgname}'|" \
    -e "s|ALL_kver=.*|ALL_kver=\"${kerver}-rockchip64\"|" \
    -i "${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset"

  # install pacman hook for initramfs regeneration
  install -D -m644 "${srcdir}/60-linux-aarch64.hook" "${pkgdir}/usr/share/libalpm/hooks/60-${pkgname}.hook"
  install -D -m644 "${srcdir}/90-linux-aarch64.hook" "${pkgdir}/usr/share/libalpm/hooks/90-${pkgname}.hook"
  sed "s|%PKGNAME%|${pkgname}|g" -i "${pkgdir}/usr/share/libalpm/hooks/60-${pkgname}.hook"
  sed "s|%PKGVER%|${kerver}-rockchip64|g" -i "${pkgdir}/usr/share/libalpm/hooks/60-${pkgname}.hook"
  sed "s|%PKGNAME%|${pkgname}|g" -i "${pkgdir}/usr/share/libalpm/hooks/90-${pkgname}.hook"

  # Now we call depmod...
  depmod -b "$pkgdir" -F "boot/System.map-${kerver}-rockchip64" "${kerver}-rockchip64"

  # move module tree /lib -> /usr/lib
  mkdir -p "${pkgdir}/usr"
  cp -r "$pkgdir/lib" "$pkgdir/usr"
  rm -rf "$pkgdir/lib"

}

